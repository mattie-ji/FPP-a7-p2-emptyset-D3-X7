// Generators and relations of the group is from https://www.maths.usyd.edu.au/u/donaldc/fakeprojectiveplanes/bargammapresentations.txt

print "\nFor the class (a=7,p=2,\emptyset)";
print "********************************";

// The group $\bar\Gamma$:

F<xz,xb>:=FreeGroup(2);
relnlist:={
xz^7,
(xb^-2*xz^1)^3,
(xb^2*xz^-2*xb^2*xz^2)^3,
(xb^2*xz^-2*xb^2*xz^4)^3,
xb^3*xz^-2*xb^-1*xz^2*xb^-2*xz^1,
xb^3*xz^1*xb^3*xz^3*xb*xz^2*xb^-1*xz^-1,
xb^3*xz^2*xb^2*xz^-2*xb^-1*xz^-1*xb^-3*xz^1*xb^-1*xz^-1};

G,psi:=quo<F|relnlist>;
z:=psi(xz);
b:=psi(xb);

print "\nFor the fpp (a=7,p=2,\emptyset,D_3 2_7)";
print "**************************************";

// generators of the fundamental group:
s1:=b^3;
s2:=(z*b*z^-1)^3;
s3:=b*z*b^2*z^-2;
s4:=z*b*z^3*b^-1;

// Here is the fundamental group:
GY:=sub< G | s1,s2,s3,s4>;
print "Index of GY in G is",Index(G,GY);
print "AbelianQuotientInvariants(GY) =",AbelianQuotientInvariants(GY);

print "\nFor the fpp (a=7,p=2,\emptyset,D_3 X_7)";
print "**************************************";

// generators of the fundamental group

s5:=b^3;
s6:=z*b^3*z;
s7:=b*z^2*b^-1*z;

// Here is the fundamental group:
GX:=sub< G | s5,s6,s7>;
print "Index of GX in G is",Index(G,GX);
print "AbelianQuotientInvariants(GX) =",AbelianQuotientInvariants(GX);


// Defining GZ:
GZ:=CommutatorSubgroup(GX);
// The next command also verifies that GZ is a subgroup of GY
Index(GY,GZ);

// Checking that GZ is a normal subgroup of G
IsNormal(G,GZ);
H:=G/GZ;
#(H);


t1:=b^3;
t2:=b*z*b^2*z^(-2)*b^3;
t3:=b*z^2*b^(-1)*z;
t4:=b^4;

// We see that the following four elements generate the quotient subgroup and satisfy 
// the relations of the semidirect product
Index(G,sub<G | GZ, t1, t2, t3, t4>);  // output = 1


Index(sub<G | GZ, t1^2,t2^7,t1*t2*t1*t2,t3^7,t1*t3*t1^(-1)*t3^(-1),t2*t3*t2^(-1)*t3^(-1)>,GZ); 
// output = 1
Index(sub<G | GZ, t4^3,t1*t4*t1^(-1)*t4^(-1), t4*t2*t4^(-1)*t2^3, t4*t3*t4^(-1)*t3^5>,GZ); 
//output = 1


// We verify that t1 and t2 generate GY/GZ and t1 and t3 generate GX/GZ
Index(sub<G | GZ, t1, t2>, GY);
Index(sub<G | GZ, t1, t3>, GX);


// Checking finiteness of abelianization
Index(GZ,CommutatorSubgroup(GZ));

